!Rui Wen 2020.12.29
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
MODULE RKCK_MOD
CONTAINS
SUBROUTINE RKCK(Y,DYDX,X,H,YOUT,YERR)

  USE DERIVS_MOD
  IMPLICIT NONE

  REAL(16),INTENT(IN) :: H,X,DYDX(:),Y(:)
  REAL(16),INTENT(OUT) :: YERR(:),YOUT(:)
  REAL(16),DIMENSION(SIZE(Y)) :: AK2,AK3,AK4,AK5,AK6
  REAL(16) :: YTEMP(SIZE(Y))
  REAL(16),PARAMETER :: A2=0.2Q+0,A3=0.3Q+0,A4=0.6Q+0,A5=1.0Q+0,A6=0.875Q+0
  REAL(16),PARAMETER :: B21=0.2Q+0,B31=3.Q+0/40.Q+0,B32=9.Q+0/40.Q+0,B41=0.3Q+0,B42=-0.9Q+0,B43=1.2Q+0
  REAL(16),PARAMETER :: B51=-11.Q+0/54.Q+0,B52=2.5Q+0,B53=-70.Q+0/27.Q+0,B54=35.Q+0/27.Q+0
  REAL(16),PARAMETER :: B61=1631.Q+0/55296.Q+0,B62=175.Q+0/512.Q+0,B63=575.Q+0/13824.Q+0
  REAL(16),PARAMETER :: B64=44275.Q+0/110592.Q+0,B65=253.Q+0/4096.Q+0
  REAL(16),PARAMETER :: C1=37.Q+0/378.Q+0,C3=250.Q+0/621.Q+0,C4=125.Q+0/594.Q+0,C6=512.Q+0/1771.Q+0
  REAL(16),PARAMETER :: DC1=C1-2825.Q+0/27648.Q+0,DC3=C3-18575.Q+0/48384.Q+0
  REAL(16),PARAMETER :: DC4=C4-13525.Q+0/55296.Q+0,DC5=-277.Q+0/14336.Q+0,DC6=C6-0.25Q+0
  YTEMP=Y+B21*H*DYDX
  CALL DERIVS(X+A2*H,YTEMP,AK2)
  YTEMP=Y+H*(B31*DYDX+B32*AK2)
  CALL DERIVS(X+A3*H,YTEMP,AK3)
  YTEMP=Y+H*(B41*DYDX+B42*AK2+B43*AK3)
  CALL DERIVS(X+A4*H,YTEMP,AK4)
  YTEMP=Y+H*(B51*DYDX+B52*AK2+B53*AK3+B54*AK4)
  CALL DERIVS(X+A5*H,YTEMP,AK5)
  YTEMP=Y+H*(B61*DYDX+B62*AK2+B63*AK3+B64*AK4+B65*AK5)
  CALL DERIVS(X+A6*H,YTEMP,AK6)
  YOUT=Y+H*(C1*DYDX+C3*AK3+C4*AK4+C6*AK6)
  YERR=H*(DC1*DYDX+DC3*AK3+DC4*AK4+DC5*AK5+DC6*AK6)
END
END MODULE RKCK_MOD
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
MODULE RKQS_MOD
CONTAINS
SUBROUTINE RKQS(Y,DYDX,X,HTRY,EPS,YSCAL,HDID,HNEXT)

  USE RKCK_MOD
  USE ERRORSHOW

  IMPLICIT NONE
  REAL(16),INTENT(INOUT) :: X,Y(:)
  REAL(16),INTENT(IN) :: HTRY,EPS
  REAL(16),INTENT(OUT) :: HDID,HNEXT
  REAL(16),INTENT(IN) :: DYDX(:),YSCAL(:)
  REAL(16) :: ERRMAX,H,HTEMP,XNEW
  REAL(16),DIMENSION(SIZE(Y)) :: YERR,YTEMP
  REAL(16),PARAMETER :: SAFETY=0.9Q+0,PGROW=-0.2Q+0,PSHRNK=-0.25Q+0,ERRCON=1.89Q-4
  LOGICAL :: CHECK

  H=HTRY
  CHECK=.TRUE.
  DO WHILE(CHECK)
    CALL RKCK(Y,DYDX,X,H,YTEMP,YERR)
    ERRMAX=MAXVAL(ABS(YERR/YSCAL))/EPS
    IF(ERRMAX <= 1.0) THEN 
      CHECK=.FALSE.
    ELSE
      HTEMP=SAFETY*H*(ERRMAX**PSHRNK)
      H=SIGN(MAX(ABS(HTEMP),0.1Q+0*ABS(H)),H)
      XNEW=X+H
      IF(XNEW.EQ.X) CALL NRERROR('STEPSIZE UNDERFLOW IN RKQS')
    END IF
  END DO
  IF(ERRMAX.GT.ERRCON)THEN
    HNEXT=SAFETY*H*(ERRMAX**PGROW)
  ELSE
    HNEXT=5.Q+0*H
  END IF
  HDID=H
  X=X+H
  Y=YTEMP
END
END MODULE RKQS_MOD
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
MODULE ODE_MOD
CONTAINS
SUBROUTINE ODEINT(YSTART,X1,X2,EPS,H1,HMIN)

  USE ODESAVE_COM
  USE ODEFUN_COM

  USE ERRORSHOW
  USE DERIVS_MOD
  USE RKQS_MOD
  IMPLICIT NONE

  REAL(16),INTENT(IN) :: X1,X2,EPS,H1,HMIN
  REAL(16),INTENT(INOUT) :: YSTART(:)
  REAL(16),PARAMETER :: MINTINY=1.Q-30
  REAL(16) :: H,HDID,HNEXT,X,XSAV
  REAL(16),DIMENSION(SIZE(YSTART)) :: DYDX,Y,YSCAL
  INTEGER(2),PARAMETER :: MAXSTP=10000
  INTEGER(2) :: I,NSTP
 
  X=X1
  H=SIGN(H1,X2-X1)
  KOUNT=0
  Y=YSTART
  IF (KMAX.GT.0) XSAV=X-2.Q+0*DXSAV
  DO NSTP=1,MAXSTP
    CALL DERIVS(X,Y,DYDX)
    YSCAL=ABS(Y)+ABS(H*DYDX)+MINTINY
    IF(KMAX.GT.0 .AND. ABS(X-XSAV).GT.ABS(DXSAV)) THEN
      IF(KOUNT.LT.KMAX-1)THEN
        KOUNT=KOUNT+1
        XP(KOUNT)=X
        YP(:,KOUNT)=Y
        FUNP(:,KOUNT)=FUNSAVE
        XSAV=X
      ENDIF
    ENDIF
    IF((X+H-X2)*(X+H-X1).GT.0.Q+0) H=X2-X
    CALL RKQS(Y,DYDX,X,H,EPS,YSCAL,HDID,HNEXT)
    IF((X-X2)*(X2-X1).GE.0.Q+0)THEN
      YSTART=Y
      IF(KMAX.NE.0)THEN
        KOUNT=KOUNT+1
        XP(KOUNT)=X
        YP(:,KOUNT)=Y
        FUNP(:,KOUNT)=FUNSAVE
      ENDIF
      RETURN
    ENDIF
    IF(ABS(HNEXT).LT.HMIN) CALL NRERROR('STEPSIZE SMALLER THAN MINIMUM IN ODEINT')
    H=HNEXT
  END DO
  CALL NRERROR('TOO MANY STEPS IN ODEINT')
END SUBROUTINE ODEINT
END MODULE ODE_MOD
